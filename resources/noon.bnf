<top> = abstract-chord | concrete-chord | abstract-mode | concrete-mode

abstract-chord = base-structure structure-modifiers?

abstract-mode = mode mode-alterations? structure-shorthand?

concrete-chord = (degree | root) base-structure structure-modifiers?

concrete-mode = (degree | root) mode mode-alterations? structure-shorthand?

(* MISC ---------------------------------------------------------------------------------------- *)

<separator> = <'.'>
augmented-fifth = <'+'>

(* DEGREE -------------------------------------------------------------------------------------- *)

degree = alteration roman-degree

<alteration> = sharp | bemol | double-bemol | double-sharp | natural

  sharp = <'#'>
  bemol = <'b'>
  double-sharp = < 'x'>
  double-bemol = <'bb' | 'o'>
  natural = <'♮' | 'n' | ''>

<roman-degree> = one | two | three | four | five | six | seven

  one = <'I'>
  two = <'II'>
  three = <'III'>
  four = <'IV'>
  five = <'V'>
  six = <'VI'>
  seven = <'VII'>

<mode-degree> = second | third | fourth | fifth | sixth | seventh

  second = <'2' | '9'>
  third = <'3' | '10'>
  fourth = <'4' | '11'>
  fifth = <'5'>
  sixth = <'6' | '13'>
  seventh = <'7'>


(* ROOT ---------------------------------------------------------------------------------------- *)

root = pitch-class alteration

  <pitch-class> = C | D | E | F | G | A | B

    C = <'C'>
    D = <'D'>
    E = <'E'>
    F = <'F'>
    G = <'G'>
    A = <'A'>
    B = <'B'>


(* BASE STRUCTURE ------------------------------------------------------------------------------ *)


base-structure = tetrad / triad


<tetrad> = major-seventh | minor-major-seventh | minor-seventh | dominant | half-diminished

  major-seventh = <'Δ' | 'M7'>
  diminished-seventh = <'o7'>
  minor-seventh = <'m7'>
  dominant = <'7'>
  half-diminished = <'ø'>
  minor-major-seventh = <'mΔ' | 'mM7'>

<triad> = diminished / augmented / minor / major

  diminished = <'o'>
  augmented = <'+'>
  minor = <'m'>
  major = <'M'> | <''>


(* MODIFERS ----------------------------------------------------------------------------------- *)


structure-modifiers = augmented-structure? structure-modifier* augmented-structure?

  <structure-modifier> = separator? (structure-omission | structure-addition | structure-suspension)


structure-suspension = (sus2 | sus4)

  sus2 = <'sus2'>
  sus4 = <'sus4'>


structure-omission = <'omit'> (omit1 | omit3 | omit5)

  omit1 = <'1'>
  omit3 = <'3'>
  omit5 = <'5'>

augmented-structure = <augmented-fifth>
structure-addition = alteration mode-degree


(* MODES ------------------------------------------------------------------------------------- *)


mode = mode-name

<mode-name> = ionian | dorian | phrygian | mixolydian | lydian | aeolian | locrian |
              harmonic-minor | melodic-minor | altered | harmonic-major |
              double-harmonic | ultraphrygian | hungarian-minor | oriental | ultralocrian

  ionian = <'ionian'>
  dorian = <'dorian'>
  phrygian = <'phrygian'>
  lydian = <'lydian'>
  mixolydian = <'mixolydian'>
  aeolian = <'aeolian'>
  locrian = <'locrian'>
  harmonic-minor = <'harmonic-minor'>
  melodic-minor = <'melodic-minor'>
  altered = <'superlocrian' | 'altered'>
  harmonic-major = <'harmonic-major'>
  double-harmonic = <'double-harmonic'>
  ultraphrygian = <'ultraphrygian'>
  hungarian-minor = <'hungarian-minor'>
  oriental = <'oriental'>
  ultralocrian = <'ultralocrian'>

mode-alterations = augmented-fifth? altered-degree* augmented-fifth?
  altered-degree = alteration mode-degree

structure-shorthand = <separator> <'s'> structure-digit+
  <structure-digit> = '1' | '2' | '3' | '4' | '5' | '6' | '7'
