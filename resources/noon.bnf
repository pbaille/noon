<top> = abstract-chord | concrete-chord | abstract-mode | concrete-mode

abstract-chord = base-structure structure-modifiers?

abstract-mode = mode mode-alterations?

concrete-chord = (degree | root) base-structure structure-modifiers?

concrete-mode = (degree | root) mode mode-alterations?

<separator> = <'.'>

(* DEGREE -------------------------------------------------------------------------------------- *)

degree = alteration roman-degree

<alteration> = sharp | bemol | double-bemol | double-sharp | natural

  sharp = <'#'>
  bemol = <'b'>
  double-sharp = < 'x'>
  double-bemol = <'bb' | 'o'>
  natural = <'♮' | 'n' | ''>

<roman-degree> = one | two | three | four | five | six | seven

  one = <'I'>
  two = <'II'>
  three = <'III'>
  four = <'IV'>
  five = <'V'>
  six = <'VI'>
  seven = <'VII'>

<mode-degree> = second | third | fourth | fifth | sixth | seventh

  second = <'2' | '9'>
  third = <'3' | '10'>
  fourth = <'4' | '11'>
  fifth = <'5'>
  sixth = <'6' | '13'>
  seventh = <'7'>


(* ROOT ---------------------------------------------------------------------------------------- *)

root = pitch-class alteration

  <pitch-class> = C | D | E | F | G | A | B

    C = <'C'>
    D = <'D'>
    E = <'E'>
    F = <'F'>
    G = <'G'>
    A = <'A'>
    B = <'B'>


(* BASE STRUCTURE ------------------------------------------------------------------------------ *)


base-structure = (tetrad / triad)


<tetrad> = major-seventh | minor-major-seventh | minor-seventh | minor-sixth | major-sixth |
           augmented-major-seventh | dominant | augmented-seventh | half-diminished

  major-seventh = <'Δ' | 'M7'>
  diminished-seventh = <'o7'>
  minor-seventh = <'m7'>
  minor-sixth = <'m6'>
  major-sixth = <'6'>
  dominant = <'7'>
  half-diminished = <'ø' | 'm7b5'>
  augmented-major-seventh = <'Δ+' | 'M7+'>
  minor-major-seventh = <'mΔ' | 'mM7'>
  augmented-seventh = <'7+'>


<triad> = diminished | augmented | minor | major

  diminished = <'o'>
  augmented = <'+'>
  minor = <'m'>
  major = <'M'> | <''>


(* MODIFERS ----------------------------------------------------------------------------------- *)


structure-modifiers = structure-modifier+

  <structure-modifier> = separator? (structure-omission | structure-addition | structure-suspension)


structure-suspension = (sus2 | sus4)

  sus2 = <'sus2'>
  sus4 = <'sus4'>


structure-omission = 'omit' omited-degree

  omited-degree = '1' | '3' | '5'


structure-addition = alteration mode-degree


(* MODES ------------------------------------------------------------------------------------- *)


mode = major-mode | melodic-minor-mode | harmonic-minor-mode | harmonic-major-mode | double-harmonic-mode


<major-mode> = ionian | dorian | phrygian | mixolydian | aeolian | locrian

  ionian = <'ionian'>
  dorian = <'dorian'>
  phrygian = <'phrygian'>
  lydian = <'lydian'>
  mixolydian = <'mixolydian'>
  aeolian = <'aeolian'>
  locrian = <'locrian'>


<harmonic-minor-mode> = harmonic-minor | locrian-natural6 | ionian-augmented | dorian-sharp4 |
                        phrygian-natural3 | lydian-sharp2 | altered-diminished7

  harmonic-minor = <'harmonic-minor'>
  locrian-natural6 = <'locrian6'>
  ionian-augmented = <'ionia#5' | 'ionian+'>
  dorian-sharp4 = <'dorian#4'>
  phrygian-natural3 = <'phrygian-dominant' | 'phrygian3'>
  lydian-sharp2 = <'lydian#2'>
  altered-diminished7 = <'alteredbb7' | 'alteredo7' | 'altered-diminished'>


<melodic-minor-mode> = melodic-minor | dorian-flat2 | lydian-augmented | lydian-dominant |
                       mixolydian-flat6 | locrian-natural2 | altered

  melodic-minor = <'melodic-minor'>
  dorian-flat2 = <'dorianb2' | 'phrygian6'>
  lydian-augmented = <'lydian-augmented' | 'lydian+' | 'lydian#5'>
  lydian-dominant = <'lydian-dominant' | 'bartok' | 'lydianb7' | 'mixolydian#4'>
  mixolydian-flat6 = <'mixolydianb6'>
  locrian-natural2 = <'locrian2'>
  altered = <'superlocrian' | 'altered'>


<harmonic-major-mode> = harmonic-major | dorian-flat5 | phrygian-flat4 | lydian-flat3 |
                        mixolydian-flat2 | lydian-augmented-sharp2 | locrian-diminished7

  harmonic-major = <'harmonic-major'>
  dorian-flat5 = <'dorianb5'>
  phrygian-flat4 = <'phrygianb4'>
  lydian-flat3 = <'lydian-minor' | 'lydianb3' | 'lydianm'>
  mixolydian-flat2 = <'mixolydianb2'>
  lydian-augmented-sharp2 = <'lydian+#2' | 'lydian#5#2'>
  locrian-diminished7 = <'locrian-diminished' | 'locriano7' | 'locrianbb7'>


<double-harmonic-mode> = double-harmonic | lydian-sharp2-sharp6 | ultraphygian | hungarian-minor |
                         oriental | ionian-augmented-sharp2 | ultralocrian

  double-harmonic = <'double-harmonic'>
  lydian-sharp2-sharp6 = <'lydian#2#6'>
  ultraphygian = <'ultraphygian' | 'phrygianb4o7'>
  hungarian-minor = <'hungarian-minor' | 'harmonic-minor#4'>
  oriental = <'oriental' | 'mixolydianb5b2'>
  ionian-augmented-sharp2 = <'ionian#5#2' | 'ionian+#2'>
  ultralocrian = <'ultralocrian' | 'locrianb2bb7' | 'locrianb2o7'>

mode-alterations = mode-alteration+
  mode-alteration = alteration mode-degree
